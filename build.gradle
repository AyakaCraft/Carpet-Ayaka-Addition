plugins {
    id 'fabric-loom' version '1.9-SNAPSHOT' apply false

    // https://github.com/ReplayMod/preprocessor
    // https://github.com/Fallen-Breath/preprocessor
    id 'com.replaymod.preprocess' version '9d21b334a7'

    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false

    id "com.modrinth.minotaur" version "2.+" apply false
}

version = project.mod_version
group = project.maven_group

preprocess {
    strictExtraMappings.set(false)

    def mc11605 = createNode("1.16.5", 1_16_05, "")
    def mc11701 = createNode("1.17.1", 1_17_01, "")
    def mc11802 = createNode("1.18.2", 1_18_02, "")
    def mc11904 = createNode("1.19.4", 1_19_04, "")
    def mc12001 = createNode("1.20.1", 1_20_01, "")
    def mc12006 = createNode("1.20.6", 1_20_06, "")

    mc12001.link(mc11904, null)
    mc11904.link(mc11802, file("versions/mapping_11904_11802.txt"))
    mc11802.link(mc11701, file("versions/mapping_11802_11701.txt"))
    mc11701.link(mc11605, null)

    mc12001.link(mc12006, null)
}

tasks.register('buildAndGather') {
    subprojects {
        dependsOn project.tasks.named('build').get()
    }
    doFirst {
        println 'Gathering builds'
        def buildLibs = {
            p -> p.buildDir.toPath().resolve('libs')
        }
        delete fileTree(buildLibs(rootProject)) {
            include '*'
        }
        subprojects {
            copy {
                from(buildLibs(project)) {
                    include '*.jar'
                    exclude '*-dev.jar', '*-sources.jar', '*-shadow.jar'
                }
                into buildLibs(rootProject)
                duplicatesStrategy DuplicatesStrategy.INCLUDE
            }
        }
    }
}

tasks.register("publishToModrinth") {
    mustRunAfter project.tasks.named('buildAndGather').get()
    subprojects {
        dependsOn project.tasks.named('modrinth').get()
    }
}
